apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: 'application'
apply plugin: "maven-publish"
apply plugin: 'signing'

mainClassName = "ru.pocketbyte.locolaser.Main"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'com.beust:jcommander:1.48'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
    manifest {
        attributes('Main-Class': "$mainClassName")
    }

    // Include all libraries into result JAR file.
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task generateJavadocs(type: Javadoc) {
    source {
        sourceSets.main.allJava
    }
    failOnError = false
}

task sourceDoc(type: Jar, dependsOn: generateJavadocs) {
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            name = "Sonatype"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = project.sonatype_user
                password = project.sonatype_password
            }
        }
    }
    publications {
        locolaser(MavenPublication) {
            from components.java
            groupId 'ru.pocketbyte.locolaser'
            artifactId 'core'
            version project.version
            pom {
                withXml {
                    def root = asNode()
                    root.remove(root.get('dependencies'))
                }

                name = 'locolaser-core'
                description = 'Core library of LocoLaser tool'
                url = 'https://github.com/PocketByte/LocoLaser'

                issueManagement {
                    url = "https://github.com/PocketByte/LocoLaser/issues"
                }
                scm {
                    url = "https://github.com/PocketByte/LocoLaser.git"
                }

                developers {
                    developer {
                        organization = "PocketByte"
                        organizationUrl = "pocketbyte.ru"
                        email = "mail@pocketbyte.ru"
                    }
                    developer {
                        name = "Denis Shurygin"
                        email = "sdi.linch@gmail.com"
                    }
                }

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
            artifact sourceJar {
                classifier "sources"
            }
            artifact sourceDoc {
                classifier "javadoc"
            }
        }
    }
}

signing {
    sign publishing.publications.locolaser
}