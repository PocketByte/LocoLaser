apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: 'kotlin'
apply plugin: "maven-publish"
apply plugin: 'signing'

def kotlin_poet_version = '0.7.0'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.squareup:kotlinpoet:$kotlin_poet_version"
    noJarCompile project(':core')
    noJarCompile project(':resource-mobile')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task generateJavadocs(type: Javadoc) {
    source {
        sourceSets.main.allJava
    }
    failOnError = false
}

task sourceDoc(type: Jar, dependsOn: generateJavadocs) {
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            name = "Sonatype"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = project.sonatype_user
                password = project.sonatype_password
            }
        }
    }
    publications {
        locolaser(MavenPublication) {
            from components.java
            groupId 'ru.pocketbyte.locolaser'
            artifactId project.name
            version project.version
            pom {
                withXml {
                    def root = asNode()
                    root.remove(root.get('dependencies'))
                    def dependencies = root.appendNode('dependencies')

                    // Add core dependency
                    def coreDependency = dependencies.appendNode('dependency')
                    coreDependency.appendNode('groupId', 'ru.pocketbyte.locolaser')
                    coreDependency.appendNode('artifactId', 'core')
                    coreDependency.appendNode('version', project.version)
                    coreDependency.appendNode('scope', 'runtime')

                    // Add mobile module dependency
                    def mobileDependency = dependencies.appendNode('dependency')
                    mobileDependency.appendNode('groupId', 'ru.pocketbyte.locolaser')
                    mobileDependency.appendNode('artifactId', 'resource-mobile')
                    mobileDependency.appendNode('version', project.version)
                    mobileDependency.appendNode('scope', 'runtime')

                    // Add Kotlin Poet dependency
                    def poetDependency = dependencies.appendNode('dependency')
                    poetDependency.appendNode('groupId', 'com.squareup')
                    poetDependency.appendNode('artifactId', 'kotlinpoet')
                    poetDependency.appendNode('version', kotlin_poet_version)
                    poetDependency.appendNode('scope', 'runtime')
                }

                name = "locolaser-${project.name}"
                description = 'Implementation of platform for LocoLaser tool to work with Kotlin MPP projects.'
                url = 'https://github.com/PocketByte/LocoLaser'

                issueManagement {
                    url = "https://github.com/PocketByte/LocoLaser/issues"
                }
                scm {
                    url = "https://github.com/PocketByte/LocoLaser.git"
                }

                developers {
                    developer {
                        organization = "PocketByte"
                        organizationUrl = "pocketbyte.ru"
                        email = "mail@pocketbyte.ru"
                    }
                    developer {
                        name = "Denis Shurygin"
                        email = "sdi.linch@gmail.com"
                    }
                }

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
            artifact sourceJar {
                classifier "sources"
            }
            artifact sourceDoc {
                classifier "javadoc"
            }
        }
    }
}

signing {
    sign publishing.publications.locolaser
}